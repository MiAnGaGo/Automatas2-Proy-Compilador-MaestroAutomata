package fuentes;

import java_cup.runtime.Symbol;
import Interfaz.Interfaz_c3d;
import Interfaz.Imagen;
import Interfaz.ImagenAC;
import java.util.Stack;

//PARSER 
parser code
{://Codigo visible
   
    
    public void syntax_error(Symbol s){
        //Interfaz_c3d.notificar_er("Error R de sintaxis: "+ s.value +" Linea "+(s.left+1)+" columna "+(s.right+1) );
    }

    public void unrecovered_syntax_error(Symbol s) throws java.lang.Exception{ 
        //Interfaz_c3d.notificar_er("Error NR de sintaxis: "+ s.value +" Linea "+(s.left+1)+" columna "+(s.right+1) );
    }
   
:}

action code
{://Codigo de acciones vamos a simular atrubutos heredados con variables globales
    String heredado="";
    String aux="";
    Stack<Display> display = new Stack<>();
    Display tmp;
    String estados="";
    String conEstados="";
    String alfabeto="";
    String maines="";
    String transicion1="";
    String transicion2="";
    String cad="";
    String cadAutomata="";
    String cadAutomataCorr="";
    private SimuladorArbolBinario simulador = new SimuladorArbolBinario();

    
:}

terminal String COMENTARIOS,ID,ENTERO,DECIMAL,MAS,MENOS,OREXP, POR, DIV, POT, ALFABETO, AUTOMATA, BOOLEANO, CONJUNTO, ESTADO, EXPRESION, FIN, FLOTANTE, GRAFICO, INI, MOSTRAR, NUEVO, FUNCION, VALIDAR, CON, AFD, AFN, EINICIAL, EFINAL, TRANSICION, HACIA, ER, MTABLA, ORDEN, OPRINPAL;
terminal String ABOVE,BELOW,ABOVEE,BELOWE,EQUAL,NEQUAL;
terminal String OR,AND,XOR,NOT,NAND, INC, DEC; 
terminal INT1,CHAR1, PAR_A, PAR_C, LLAV_A, LLAV_C, COMA, PYCOMA, DPUNTO, ASIG, ELEMENTO, ELEMENTOESTADO, ELEMENTOESTADO2,ENTER;
terminal MIENTRAS, HASTA, REPETIR, HACER, SI, SINO, SELEC, CASO, DEFECTO, PARA, DETENER, CONTINUA;


non terminal String EXP,OP_REL, OPER, VAL;
non terminal  INICIO,SENTENCIAS, ELEMENTOS, ELEMENTOSESTADOS, ELEMENTOSESTADOS2,ELEMENTOSTRANSICION, MOSTRARGRAFICO, MOSTRARTABLA,IMPLEFUNCION, IMPLEOPRINCIPAL, SENESPECIALES, DEFEXPRESION, METODOS, TIPO, S, L,DECLARA, ASIGNA, WHILE, DO_WHILE,  DECESTADOS, DECALFABETO, DECESTADOI, DECESTADOSFIN, DECTRANSICIONES, FOR, IF, ELSE, SELECT, CASOS, C, DEF, REPEAT;
non terminal Atributo_C3D LOG,REL;


//agregamos la precedencia

precedence left OR,XOR;
precedence left AND,NAND;
precedence right NOT;
precedence left ABOVE, BELOW, ABOVEE, BELOWE, EQUAL, NEQUAL;
precedence left MAS, MENOS;
precedence left POR, DIV; 
precedence right POT; 


start with INICIO;

INICIO ::= SENTENCIAS FIN PYCOMA error
            {:
               Generador_tmp.reset();
               Interfaz_c3d.notificar_er("Error sintactico el programa debe comenzar con la palabra Inicio; ");
            :}
            |INI PYCOMA SENTENCIAS FIN PYCOMA
            {:
                
                Interfaz_c3d.notificarG("Inicio ->\n Inicio SENTENCIAS Fin  \n");
                Imagen.getruta("/inicio;sentenciafin;.png");
                cadAutomataCorr = cadAutomataCorr+"/inicio;sentenciafin;.png"+"\n";
                Interfaz_c3d.txt_AutomataCorrecto.setText(cadAutomataCorr);
            :}
            
;

SENTENCIAS ::= S SENTENCIAS 
    {:
    Interfaz_c3d.notificarG("SENTENCIAS -> \n    ");
    :}
    |S
    {:
    Interfaz_c3d.notificarG(" S -> \n");
    :}
;

S ::= LOG:log
            {:
                Interfaz_c3d.notificar(log.etq_true+":\n"+log.etq_false+":");
                //Interfaz_c3d.notificar(" ");
            :} 
    | DECLARA
            {:
               // Interfaz_c3d.notificar(" DECLARACION() ");
            :}
    | DECALFABETO
            {:
                //Interfaz_c3d.notificar(" DECLARACION_ALFABETO()\n");
                //Interfaz_c3d.notificarG("     DECALFABETO -> ");
            :}
    | DECESTADOS
            {:
                //Interfaz_c3d.notificar(" DECLARACION_ESTADOS()\n");
                //Interfaz_c3d.notificarG("   SENTENCIA -> \n      DECLARA_ESTADOS ->");
            :}
    | DECESTADOI
            {:
                //Interfaz_c3d.notificar(" DECLARACION_ESTADO_I()");
            :}
    | DECESTADOSFIN
            {:
                //Interfaz_c3d.notificar(" DECLARACION_ESTADOS_F()");
            :}
    | DECTRANSICIONES
            {:
               // Interfaz_c3d.notificar(" DECLARACION_TRANSICIONES()");
            :}
    | MOSTRARGRAFICO
            {:
                //Interfaz_c3d.notificar(" MOSTRAR_GRAFICO() ");
            :}
    | MOSTRARTABLA
            {:
                //Interfaz_c3d.notificar(" ");
            :}
    | IMPLEFUNCION
            {:
                //Interfaz_c3d.notificar(" FUNCION ");
            :}
    | IMPLEOPRINCIPAL
            {:
                //Interfaz_c3d.notificar(" Main()");
            :}
    | WHILE
            {:
                //Interfaz_c3d.notificar(" ");
            :}
    | ASIGNA
            {:
                //Interfaz_c3d.notificar(" ");
            :}
    | FOR
            {:
                //Interfaz_c3d.notificar(" ");
            :}
    | IF
            {:
                //Interfaz_c3d.notificar(" ");
            :}
    | SELECT
            {:
                //Interfaz_c3d.notificar(" ");
            :}
    | REPEAT
            {:
                 //Interfaz_c3d.notificar(" "); 
            :}
    | DO_WHILE
            {:
                //Interfaz_c3d.notificar(" ");
            :}
    | DETENER PYCOMA
            {: //vamos a validar cuando aparecen estas expresiones
                if(display.isEmpty()){
                    Interfaz_c3d.notificar("ERROR: no existe una sentecia envolvente valida para detener");
                }else{
                    Interfaz_c3d.notificar("goto "+display.peek().et_salida);
                }
            :}
    | CONTINUA PYCOMA
            {:
                if(display.isEmpty()){
                    Interfaz_c3d.notificar("ERROR: no existe una sentecia envolvente valida para detener");
                }else{
                    if(display.peek().tipo==2){
                        Interfaz_c3d.notificar("ERROR: la sentencia envolvnete no permite continue");
                    }else{
                        Interfaz_c3d.notificar("goto "+display.peek().et_inicio);
                    }
                }
            :}
    | error PYCOMA
    | error LLAV_C
;

SELECT ::=  SELEC PAR_A EXP:exp
                {:
                    tmp= new Display(Generador_tmp.genera_etq(),Generador_tmp.genera_etq(),2); //generamos las etiquetas de inicio y salida
                    display.push(tmp);
                    Interfaz_c3d.notificar("goto "+display.peek().et_inicio);
                    heredado=exp; //valor a comparar con cada caso
                    aux=""; //cadena donde llevaremos los saltos condicionales de los casos
                :}
            PAR_C LLAV_A CASOS  DEF LLAV_C
                {:
                    Interfaz_c3d.notificar(display.peek().et_inicio+":");
                    Interfaz_c3d.notificar(aux);
                    Interfaz_c3d.notificar(display.peek().et_salida+":");
                    display.pop();
                :}
;

CASOS ::= CASOS C
        | C
;

C ::=   CASO VAL:val
            {:
                String et=Generador_tmp.genera_etq(); //generamos la etiqueta del caso
                Interfaz_c3d.notificar(et+":"); // la escribimos antes de sus sentencias
                aux+="if "+heredado+"=="+val+" goto "+et+"\n";
            :}
        DPUNTO SENTENCIAS
;

VAL ::= ENTERO:ent 
            {:
                RESULT=ent;
            :}
    |   DECIMAL:dec
            {:
                RESULT=dec;
            :}
;

DEF ::= DEFECTO 
            {:
                String et=Generador_tmp.genera_etq(); //generamos la etiqueta del caso
                Interfaz_c3d.notificar(et+":"); // la escribimos antes de sus sentencias
                aux+="goto "+et+"\n";
            :}
        DPUNTO SENTENCIAS
            {:
                //a defecto despues de sus sentencias de una vez se le agrega la etiqueta de salida
                Interfaz_c3d.notificar("goto "+display.peek().et_salida);
            :}
    | //empty
;
  

DO_WHILE ::=    HACER LLAV_A
                    {:
                        tmp =  new Display(Generador_tmp.genera_etq(),Generador_tmp.genera_etq(),1);
                        display.push(tmp);
                        aux=Generador_tmp.genera_etq();
                        Interfaz_c3d.notificar(aux+":");
                    :}
                SENTENCIAS LLAV_C MIENTRAS 
                    {:
                        Interfaz_c3d.notificar(display.peek().et_inicio+":");
                    :}
                PAR_A LOG:log PAR_C PYCOMA
                    {:
                        Interfaz_c3d.notificar(log.etq_true+":");
                        Interfaz_c3d.notificar("goto "+aux);
                        Interfaz_c3d.notificar(log.etq_false+":");
                        Interfaz_c3d.notificar(display.peek().et_salida+":");
                        display.pop();
                    :}
;

REPEAT ::=  REPETIR LLAV_A
                {:
                    tmp =  new Display(Generador_tmp.genera_etq(),Generador_tmp.genera_etq(),1);
                    display.push(tmp);
                    aux=Generador_tmp.genera_etq();
                    Interfaz_c3d.notificar(aux+":");
                :}
            SENTENCIAS LLAV_C HASTA
                {:
                    Interfaz_c3d.notificar(display.peek().et_inicio+":");
                :}
            PAR_A LOG:log PAR_C PYCOMA
                {:
                    Interfaz_c3d.notificar(log.etq_false+":");
                    Interfaz_c3d.notificar("goto "+aux);
                    Interfaz_c3d.notificar(log.etq_true+":");
                    Interfaz_c3d.notificar(display.peek().et_salida+":");
                    display.pop();
                :}
;

IF ::=  SI PAR_A LOG:log
            {:
                Interfaz_c3d.notificar(log.etq_true+":");
            :}
        PAR_C LLAV_A SENTENCIAS LLAV_C 
            {: 
                heredado=log.etq_false; 
            :}
        ELSE 
;

ELSE ::= SINO
            {:
                aux=Generador_tmp.genera_etq();
                Interfaz_c3d.notificar("goto "+aux);
                Interfaz_c3d.notificar(heredado+":");
            :}
        LLAV_A SENTENCIAS LLAV_C 
            {:
                Interfaz_c3d.notificar(aux+":");
            :}

        |
            {:
                Interfaz_c3d.notificar(heredado+":");
            :}
;

FOR ::= PARA PAR_A INT1 ID:id 
            {:
                heredado=id;
            :}  
            ASIG EXP:exp PYCOMA
            {:
                Interfaz_c3d.notificar(id+"="+exp); //asigna el valor
                tmp =  new Display(Generador_tmp.genera_etq(),Generador_tmp.genera_etq(),1);
                display.push(tmp);
                Interfaz_c3d.notificar(display.peek().et_inicio+":");
            :}
        LOG:log PYCOMA
            {:
                Interfaz_c3d.notificar(log.etq_true+":");
            :}
        OPER:op PAR_C LLAV_A SENTENCIAS LLAV_C
            {:
                Interfaz_c3d.notificar(op);    
                Interfaz_c3d.notificar("goto "+display.peek().et_inicio); //etiqueta que retorna a la condición
                Interfaz_c3d.notificar(log.etq_false+":");
                Interfaz_c3d.notificar(display.peek().et_salida+":");
                display.pop();
            :}
;

OPER ::=    INC
                {:
                    String tmp=Generador_tmp.genera_tmp(); 
                    RESULT=tmp+"="+heredado+"+1\n"+heredado+"="+tmp;
                :}
    |       DEC
                {:
                    String tmp=Generador_tmp.genera_tmp(); 
                    RESULT=tmp+"="+heredado+"-1\n"+heredado+"="+tmp;
                :}
;

WHILE ::=   MIENTRAS 
            {:
                tmp =  new Display(Generador_tmp.genera_etq(),Generador_tmp.genera_etq(),1);
                display.push(tmp);
                Interfaz_c3d.notificar(display.peek().et_inicio+":");
            :}
            PAR_A LOG:log PAR_C
            {:
                Interfaz_c3d.notificar(log.etq_true+":");
            :}
            LLAV_A SENTENCIAS LLAV_C
            {:
                Interfaz_c3d.notificar("goto "+display.peek().et_inicio); //etiqueta que retorna a la condición
                Interfaz_c3d.notificar(log.etq_false+":");
                Interfaz_c3d.notificar(display.peek().et_salida+":");
                display.pop();
            :}
;

LOG ::= LOG:rel1 OR
            {:
                Interfaz_c3d.notificar(rel1.etq_false+":");
            :}
            LOG:rel2
            {:
                RESULT = new Atributo_C3D();
                RESULT.etq_true= rel1.etq_true+","+rel2.etq_true;  
                RESULT.etq_false= rel2.etq_false;
            :}

    |   LOG:rel1 AND
            {:
                Interfaz_c3d.notificar(rel1.etq_true+":");
            :}
            LOG:rel2
            {:
                RESULT = new Atributo_C3D();
                RESULT.etq_true= rel2.etq_true;  
                RESULT.etq_false= rel1.etq_false+","+rel2.etq_false;
            :}

    |   LOG:rel1 XOR
            {:
                Interfaz_c3d.notificar(rel1.etq_false+":");
            :}
            LOG:rel2
            {:
                Interfaz_c3d.notificar(rel1.etq_true+":");
                Interfaz_c3d.notificar(rel2.aux);
                RESULT= new Atributo_C3D();
                RESULT.etq_true =rel2.etq_true;
                RESULT.etq_false=rel2.etq_false;
            :}
    |   LOG:rel1 NAND
            {:
                Interfaz_c3d.notificar(rel1.etq_true+":");
            :}
            LOG:rel2
            {:
                RESULT = new Atributo_C3D();
                RESULT.etq_true= rel1.etq_false+","+rel2.etq_false;  
                RESULT.etq_false= rel2.etq_true;
            :}

    |   NOT LOG:rel
            {:
                RESULT= new Atributo_C3D();
                RESULT.etq_true = rel.etq_false;
                RESULT.etq_false = rel.etq_true;
            :}

    |   REL:val
            {:
                RESULT=val;
            :}
;
 
REL ::= EXP:t1 OP_REL:op EXP:t2
            {:
                RESULT= new Atributo_C3D();
                RESULT.etq_true= Generador_tmp.genera_etq();
                RESULT.etq_false= Generador_tmp.genera_etq();
                //imprimimos la condicion de la expresion relacional con su saltos
                Interfaz_c3d.notificar("if "+t1+op+t2+" then goto "+RESULT.etq_true+"\ngoto "+RESULT.etq_false);
                // creamos la cadena temporal solo se usa en XOR
                RESULT.aux= "if "+t1+op+t2+" then goto "+RESULT.etq_false+"\ngoto "+RESULT.etq_true; 
            :}

    |EXP:val
            {:
                RESULT= new Atributo_C3D();
                RESULT.aux= val;
            :}
;

OP_REL::= ABOVE:sim
            {:
                RESULT=sim;
            :}
    |     BELOW:sim
            {:
                RESULT=sim;
            :}
    |     ABOVEE:sim
            {:
                RESULT=sim;
            :}
    |     BELOWE:sim
            {:
                RESULT=sim;
            :}
    |     EQUAL:sim
            {:
                RESULT=sim;
            :}
    |     NEQUAL:sim
            {:
                RESULT=sim;
            :}
; 

EXP ::= EXP:val MAS:sim EXP:val2
            {:
                cad=cad+val+""+sim+val2+"\n";
                RESULT=Generador_tmp.genera_tmp(); //sintetizo el temporal 
                Interfaz_c3d.notificar(RESULT+"="+val+sim+val2); //imprimo la cadena la operacion
                Interfaz_c3d.txt_expresion.setText(cad);
               
                                    
            :}
    |   EXP:val MENOS:sim EXP:val2
            {:
                cad=cad+val+""+sim+val2+"\n";
                RESULT=Generador_tmp.genera_tmp();
                Interfaz_c3d.notificar(RESULT+"="+val+sim+val2);
                Interfaz_c3d.txt_expresion.setText(cad);
            :}
    |   EXP:val POR:sim EXP:val2
            {:
                cad=cad+val+""+sim+val2+"\n";
                RESULT=Generador_tmp.genera_tmp();
                Interfaz_c3d.notificar(RESULT+"="+val+sim+val2);
                Interfaz_c3d.txt_expresion.setText(cad);
            :}
    |   EXP:val DIV:sim EXP:val2
            {:
                cad=cad+val+""+sim+val2+"\n";
                RESULT=Generador_tmp.genera_tmp();
                Interfaz_c3d.notificar(RESULT+"="+val+sim+val2);
                Interfaz_c3d.txt_expresion.setText(cad);
            :}
    |   EXP:val POT:sim EXP:val2
            {:
                cad=cad+val+""+sim+val2+"\n";
                RESULT=Generador_tmp.genera_tmp();
                Interfaz_c3d.notificar(RESULT+"="+val+sim+val2);
                Interfaz_c3d.txt_expresion.setText(cad);
            :}
    |   ID:val
            {:
                RESULT=val;
            :}
    |   ENTERO:val
            {:
                RESULT=val;
            :}
    |   DECIMAL:val 
            {: 
                RESULT=val;
            :}
    |MENOS:sim EXP:val
            {:
                RESULT=Generador_tmp.genera_tmp();
                Interfaz_c3d.notificar(RESULT+"="+sim+val);
            :}
    |PAR_A LOG:val PAR_C
            {:
                RESULT = val.aux;
            :}
;

ASIGNA ::= ID:id ASIG  EXP:exp PYCOMA 
            {:
                Interfaz_c3d.notificar(id+"="+exp);
            :}
;

DECLARA ::= TIPO ID PYCOMA 
            {:
                Interfaz_c3d.notificar("Reserva espacio y almacena el id de las variables.");
            :}
        | FLOTANTE:fl ID:id ASIG EXP:exp PYCOMA
            {:
                //Interfaz_c3d.notificar(t+" Asigna "+exp+" a varibales()");
                
                String cad=""+exp;
                boolean hayPunto=false;
                for( int i=0;i<cad.length(); i++ )
                if ( cad.charAt(i) == '.')                          //Detectar si hay un punto decimal en la cadena
                hayPunto=true;
                if(!(hayPunto)){
                    Interfaz_c3d.notificar_er("Error semantico: En la linea "+(flleft+1)+" columna "+(expright+1)+" tipos incompatibles se esperaba un Flotante" );
                }else{
                Interfaz_c3d.notificar(id+" = "+exp+"\n");
                }
            :}
        | INT1:in ID:id ASIG EXP:exp PYCOMA
                {:
                
                String cad=""+exp;
                boolean hayPunto=false;
                for(int i=0;i<cad.length(); i++ )
                if ( cad.charAt(i) == '.')                          //Detectar si hay un punto decimal en la cadena
                hayPunto=true;
                if((hayPunto)){
                    Interfaz_c3d.notificar_er("Error semantico: En la linea "+(inleft+1)+" columna "+(expright+1)+" tipos incompatibles se esperaba un Entero" );
                }else{
                Interfaz_c3d.notificar(id+" = "+exp+"\n");
                }
                :}
       
;

TIPO ::= CHAR1
    |   BOOLEANO
;


DECALFABETO::= ALFABETO ID:id ASIG PAR_A ELEMENTOS:e PAR_C PYCOMA
            {:
                Interfaz_c3d.notificar("     ALFABETO("+id+",[ "+alfabeto+" ])\n");
                Interfaz_c3d.notificarG("         DECALFABETO -> \n             ID -> "+id+" = ( ELEMENTOS -> "+alfabeto+"); \n"+
                                        "-----------------------------------------------------------------------\n");
                alfabeto="";
                cadAutomataCorr = cadAutomataCorr+"/AlfabetoId=(Elementos);.png"+"\n";
                Interfaz_c3d.txt_AutomataCorrecto.setText(cadAutomataCorr);
            :}
;

ELEMENTOS::= ELEMENTO:e COMA:c ELEMENTOS
    {:
        alfabeto=""+e+c+alfabeto;
    :} 
| ELEMENTO:f
    {:
        alfabeto=f+alfabeto;
    :}             
;

DECESTADOS::= ESTADO ASIG PAR_A ELEMENTOSESTADOS PAR_C PYCOMA
            {:
                Interfaz_c3d.notificarG("          DECESTADOS -> Estado = ( \n              ELEMENTOSESTADO -> "+estados+" );\n"+
                                        "-----------------------------------------------------------------------\n");
                Interfaz_c3d.notificar("     ESTADOS("+estados+")\n");
                 estados = "";
            :}
            |ESTADO ASIG PAR_A ELEMENTOSESTADOS PAR_C:parenC
            {:
                
                Interfaz_c3d.notificar("     ESTADOS("+estados+")\n");
                 estados = "";
                Interfaz_c3d.notificar_er("Error de sintaxis en la linea "+(parenCleft+1)+", falta un ; después del ')'");
Interfaz_c3d.notificarG("     DECESTADOS -> Estado = ( "+estados+")(;) ERROR\n");

            :}
;

DECESTADOI::= EINICIAL ASIG ELEMENTOESTADO:e PYCOMA
            {:
                Interfaz_c3d.notificar("     E_INICIAL("+e+")\n");
                Interfaz_c3d.notificarG("         EINICIAL -> estadoInicial -> \n       = ELEMENTOESTADO -> "+e+";\n"+
                                        "----------------------------------------------------------------------\n");
                
                cadAutomataCorr = cadAutomataCorr+"/estadoInicial=SentenciasElemento;.png"+"\n";
                Interfaz_c3d.txt_AutomataCorrecto.setText(cadAutomataCorr);
            :}
;

DECESTADOSFIN::= EFINAL ASIG PAR_A ELEMENTOSESTADOS:e PAR_C PYCOMA
            {:
                
                Interfaz_c3d.notificar("     E_FINALES("+estados+")\n");
                Interfaz_c3d.notificarG("          EFINAL -> estadosFinales -> \n       = (ELEMENTOSESTADO -> "+estados+");\n"+
                                        "----------------------------------------------------------------------\n");
                 estados = "";
            :}
;

DECTRANSICIONES::= TRANSICION ASIG PAR_A ELEMENTOSTRANSICION PAR_C PYCOMA
            {:
                Interfaz_c3d.notificar(transicion1+"\n");
                transicion1="";
                transicion2="";
                estados="";
            :}
;

ELEMENTOSESTADOS2::= ELEMENTOESTADO2:e COMA:c ELEMENTOSESTADOS2 
            {:
                 conEstados =""+e+c+conEstados;
            :}
| ELEMENTOESTADO2:f
            {:
                conEstados=f+conEstados;
            :}
;

ELEMENTOSESTADOS::= ELEMENTOESTADO:e COMA:c ELEMENTOSESTADOS 
            {:
                 estados =""+e+c+estados;
            :}
| ELEMENTOESTADO:f
            {:
                estados=f+estados;
               
            :}
;


ELEMENTOSTRANSICION::= ELEMENTOESTADO:e1 COMA ELEMENTO:e COMA ELEMENTOESTADO:e2 
            {:
                transicion1="     ("+e1+","+e+")  -->  "+e2; 
                 Interfaz_c3d.notificarG("         DECTRANSICIONES -> \n          TRANSICION -> transicion -> \n            = ( ELEMENTOSTRANSICION -> \n                ELEMENTOESTADO -> "+e1+", ELEMENTO -> "+e+", ELEMENTOESTADO -> "+e2+" );\n"+
                                        "----------------------------------------------------------------------\n");
                
               
            :}
            | LLAV_A ELEMENTOSESTADOS:e1 LLAV_C COMA ELEMENTO:e COMA ELEMENTOESTADO:e2
            {:
                    transicion1="     ({"+estados+"}"+","+e+")  -->  "+e2; 
                    estados="";
            :}
            | ELEMENTOESTADO:e1 COMA ELEMENTO:e COMA LLAV_A ELEMENTOSESTADOS:e2 LLAV_C
            {:
                    transicion1="     ("+e1+","+e+")  -->  {"+estados+"}"; 
                    estados="";
            :}
            | LLAV_A ELEMENTOSESTADOS:e1 LLAV_C COMA ELEMENTO:e COMA LLAV_A ELEMENTOSESTADOS2 LLAV_C
            {:
                
                transicion1="     ({"+estados+"}"+","+e+")  -->  {"+conEstados+"}";
                estados="";
                conEstados="";
                
            :}
;

IMPLEFUNCION::= FUNCION AUTOMATA ID:id PAR_A PAR_C LLAV_A SENTENCIAS LLAV_C 
            {:
                Interfaz_c3d.notificarG("      FUNCION_"+id+"()\n");
                if (id.equals("ejemplo1")){
                ImagenAC.getruta("/AuT1BienDelgadoConFinal.png");
                Interfaz_c3d.notificarImgAC();
                }
                if (id.equals("ejemplo2")){
                ImagenAC.getruta("/AuT2BienDelgadoConFinales.png");
                Interfaz_c3d.notificarG("Si entro en la funcion"+id+"\n");
                Interfaz_c3d.notificarImgAC();
                }
                
            :}
                | FUNCION ER ID:id PAR_A PAR_C LLAV_A SENESPECIALES LLAV_C 
            {:
                Interfaz_c3d.notificar("      FUNCION_"+id+"()\n");

            :}
;

SENESPECIALES::= DECALFABETO ENTER EXPRESION ID ASIG PAR_A DEFEXPRESION PAR_C PYCOMA
;

DEFEXPRESION::= 
;

MOSTRARGRAFICO::= GRAFICO PAR_A ID:id PAR_C PYCOMA
            {:
                Interfaz_c3d.notificar("     GRAFICO("+id+")\n");
            :}
;

MOSTRARTABLA::= MTABLA PAR_A ID PAR_C PYCOMA
;

IMPLEOPRINCIPAL::= OPRINPAL LLAV_A METODOS LLAV_C
            {:
                Interfaz_c3d.notificar("     MAIN("+maines+")\n");
                Interfaz_c3d.notificarG("IMPLEOPRINCIPAL -> \n OPRINPAL LLAV_A METODOS LLAV_C ");
            :}
;

METODOS::= ID:id PAR_A PAR_C PYCOMA ENTER METODOS 
    {:
                maines=id+","+maines;
            :}
| ID:id PAR_A PAR_C PYCOMA
{:
                maines=""+id+maines;
            :}

;
INICIO ::= INI PYCOMA SENTENCIAS FIN:fin
            {:
                Generador_tmp.reset();
                  
                  Interfaz_c3d.notificar_er("Error de sintaxis en la linea "+(finleft+1)+", se esperaba un ; después de Fin" );
                  cadAutomata = cadAutomata+"/inicio;sentenciafin.png"+"\n";
                  Interfaz_c3d.txt_Automata.setText(cadAutomata);
            :}
;


DECALFABETO::= ALFABETO ID:id ASIG PAR_A ELEMENTOS:elem PAR_C
            {:
                Interfaz_c3d.notificar_er("Error de sintaxis en la linea "+(elemleft+1)+", se esperaba un ; después del ')'" );
                //Interfaz_c3d.mandarRuta("/AlfabetoId=(Elementos).png");
                cadAutomata = cadAutomata+"/AlfabetoId=(Elementos).png"+"\n";
                Interfaz_c3d.txt_Automata.setText(cadAutomata);
                
            :}
            | ALFABETO ASIG:asig PAR_A ELEMENTOS PAR_C PYCOMA
            {:
                Interfaz_c3d.notificar_er("Error de sintaxis en la linea "+(asigleft+1)+", se esperaba un Identificador antes del signo  "+asig );
            :}
            | ALFABETO ID ASIG PAR_A ELEMENTOS PYCOMA:pcoma
            {:
                Interfaz_c3d.notificar_er("Error de sintaxis en la linea "+(pcomaleft+1)+", se esperaba un ')' antes del  "+pcoma );
            :}
            | ALFABETO ID ASIG PAR_A ELEMENTOSESTADOS:esta PYCOMA
            {:
                Interfaz_c3d.notificar_er("Error semántico en la linea "+(estaleft+1)+", los valores no son compatibles con la declaración de un alfabeto");
            :}
;

DECESTADOS::= ESTADO ID:ides ASIG PAR_A ELEMENTOSESTADOS PAR_C PYCOMA
            {:
                Interfaz_c3d.notificar_er("Error de sintaxis en la linea "+(idesleft+1)+", la estructura no utiliza un "+ides );
            :}
            | ESTADO PAR_A:paren ELEMENTOSESTADOS PAR_C PYCOMA
            {:
                Interfaz_c3d.notificar_er("Error de sintaxis en la linea "+(parenleft+1)+", se esperaba una asignacion(=) antes del '('");
            :}
            | ALFABETO:alfa ASIG PAR_A ELEMENTOSESTADOS PAR_C:parenC
            {:
                Interfaz_c3d.notificar_er("Error de sintaxis en la linea "+(alfaleft+1)+", el alfabeto no puede contener contener entre sus valores Estados");
            :}
            
            
;


IMPLEFUNCION::= FUNCION AUTOMATA PAR_A:parenAf PAR_C LLAV_A SENTENCIAS LLAV_C 
            {:
                Interfaz_c3d.notificar_er("Error de sintaxis en la linea "+(parenAfleft+1)+", se esperaba un Identificador antes del '('");
            :}
;

DECTRANSICIONES::= TRANSICION ID:idest ASIG PAR_A ELEMENTOSTRANSICION PAR_C PYCOMA
            {:
                Interfaz_c3d.notificar_er("Error de sintaxis en la linea "+(idestleft+1)+", la estructura no utiliza un "+idest );
            :}
            | TRANSICION PAR_A:parent ELEMENTOSTRANSICION PAR_C PYCOMA
            {:
               Interfaz_c3d.notificar_er("Error de sintaxis en la linea "+(parentleft+1)+", se esperaba una asignacion(=) antes del '('");
            :}
            | TRANSICION ASIG PAR_A ELEMENTOSTRANSICION PAR_C:parenCt
            {:
                Interfaz_c3d.notificar_er("Error de sintaxis en la linea "+(parenCtleft+1)+", falta un ; después del ')'");
            :}
;

DECESTADOSFIN::= EFINAL ID:idesf ASIG PAR_A ELEMENTOSESTADOS PAR_C PYCOMA
            {:
                Interfaz_c3d.notificar_er("Error de sintaxis en la linea "+(idesfleft+1)+", la estructura no utiliza un "+idesf );
            :}
            | EFINAL PAR_A:parenf ELEMENTOSESTADOS PAR_C PYCOMA
            {:
               Interfaz_c3d.notificar_er("Error de sintaxis en la linea "+(parenfleft+1)+", se esperaba una asignacion(=) antes del '('");
            :}
            | EFINAL ASIG PAR_A ELEMENTOSESTADOS PAR_C:parenCf
            {:
                Interfaz_c3d.notificar_er("Error de sintaxis en la linea "+(parenCfleft+1)+", falta un ; después del ')'");
            :}
;

DECESTADOI::= EINICIAL ASIG ELEMENTOESTADO:e 
            {:
                Interfaz_c3d.notificar_er("Error de sintaxis en la linea "+(eleft+1)+", falta un ; para terminar la sentencia");
                cadAutomata = cadAutomata+"/estadoInicial=SentenciasElemento.png"+"\n";
                Interfaz_c3d.txt_Automata.setText(cadAutomata);
            :}
;

DECLARA ::= TIPO ID:idD 
            {:
                Interfaz_c3d.notificar_er("Error de sintaxis en la linea "+(idDleft+1)+", se esperaba una ; para terminar la sentencia");
            :}
        | TIPO ID ASIG EXP:exp 
            {:
                Interfaz_c3d.notificar_er("Error de sintaxis en la linea "+(expleft+1)+", se esperaba una ; para terminar la sentencia");
            :}
       
;




